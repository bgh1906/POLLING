buildscript {
    ext {
        springBootVersion = '2.5.3'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
    }
}

ext['log4j2.version'] = '2.17.1'

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'


    group = 'com.polling'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '11'

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        /* Spring */
        implementation 'org.springframework.boot:spring-boot-starter'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        /* WEB */
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-validation'

        /* JPA */
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.8.0'
        implementation 'com.querydsl:querydsl-jpa'
        implementation 'com.querydsl:querydsl-apt'

        /* Security */
        implementation 'org.springframework.boot:spring-boot-starter-security'
        testImplementation 'org.springframework.security:spring-security-test'

        /* Jwt */
        implementation 'io.jsonwebtoken:jjwt-api:0.11.2'
        runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.2'
        runtimeOnly	'io.jsonwebtoken:jjwt-jackson:0.11.2'

        /* Redis */
        implementation 'org.springframework.boot:spring-boot-starter-data-redis'

        /* mokito */
        testImplementation 'org.mockito:mockito-core:4.4.0'

        /* gson */
        implementation 'com.google.code.gson:gson:2.8.7'


        /* lombok */
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        /* devtools */
        developmentOnly 'org.springframework.boot:spring-boot-devtools'


        /* webflux */
        implementation('org.springframework.boot:spring-boot-starter-webflux')
        testImplementation('io.projectreactor:reactor-test')

        /* yaml-importer */
//        implementation 'com.github.kingbbode:spring-boot-custom-yaml-importer:0.1.0'

    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "failed"
            exceptionFormat "full"
        }
    }
}


project(':module-api') {
    dependencies {
        api project(':module-common')
        api project(':module-core')
    }
}

project(':module-common') {
    dependencies {
        api project(':module-core')
    }
}
